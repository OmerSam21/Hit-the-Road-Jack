class Traffic {
    field Array cars;
    field int carCount;
    field int activeCarCount;
    field int seed;

    constructor Traffic new(int gameSeed) {
        var int i;
        var int startX, startY;
        var int speed, direction;
        var Car newCar;
        
        let seed = gameSeed;
        let activeCarCount = 0;
        let carCount = 12;
        let cars = Array.new(carCount);

        let i = 0;
        while (i < carCount) {
            let startX = ((i + 2) * 32) + 4;
            if (rand(2) = 0) {
                let direction = 1; 
                let startY = 0;
            }
            else {
                let direction = -1;
                let startY = 200;
            }
            let speed = rand(4) + 2; 
            let cars[i] = Car.new(startX, startY, 20, 30, speed, direction);

            let i = i + 1;
        }

        do addCars(4);
        return this;
    }

    method void dispose() {
        var int i;
        var Car c;
        let i = 0;
        while (i < carCount) {
            let c = cars[i];
            do c.dispose();
            let i = i + 1;
        }
        do cars.dispose();
        do Memory.deAlloc(this);
        return;
    }

    /** Returns a pseudo random number from 0 to (limit-1) */ 
    method int rand(int limit) {
        var int val;
        
        // Scramble the seed
        let seed = (seed * 237) + 20251;

        if (seed < 0) { let seed = -seed; }
        if (seed < 0) { let seed = 0; } // Safety for -32768
        
        let val = seed / 32; 
        return val - (limit * (val / limit));
    }

    /** Activates 'count' number of random inactive cars */
    method void addCars(int count) {
        var int i, attempts, idx;
        var Car c;
        var boolean done;

        let i = 0;
        while (i < count) {
            let done = false;
            let attempts = 0;
            
            // Try to find an inactive car
            while ((~done) & (attempts < 20)) {
                let idx = rand(carCount);
                let c = cars[idx];
                
                if (~c.isActive()) {
                    do c.activate();
                    let activeCarCount = activeCarCount + 1;
                    let done = true;
                }
                let attempts = attempts + 1;
            }
            
            // Fallback: If random failed, find the first inactive one linearly
            if (~done) {
                let idx = 0;
                while ((idx < carCount) & (~done)) {
                    let c = cars[idx];
                    if (~c.isActive()) {
                        do c.activate();
                        let activeCarCount = activeCarCount + 1;
                        let done = true;
                    }
                    let idx = idx + 1;
                }
            }
            let i = i + 1;
        }
        return;
    }

    /** Moves all cars in the fleet */
    method void moveTraffic() {
        var int i;
        var Car c;
        let i = 0;
        
        while (i < carCount) {
            let c = cars[i];
            do c.move();
            let i = i + 1;
        }
        return;
    }

    /** Checks if any car has hit the player */
    method boolean checkCollisions(Player p) {
        var int i, playerX, playerY, playerSize, col, laneIndex;
        var Car c;
        let i = 0;
        let playerX = p.getX();
        let playerY = p.getY();
        let playerSize = p.getSize();
    
        let col = playerX / 32;
        let laneIndex = col - 2;

        // Check collision only with a the car that that the player is currently in
        if ((laneIndex > -1) & (laneIndex < carCount)) {
            let c = cars[laneIndex];
            if (c.checkCollision(playerX, playerY, playerSize)) {
                return true; 
            }
        }
        return false;
    }

    /* Increase difficulty by making cars faster*/
    method void increaseDifficulty() {
        var int i;
        var Car c;
        let i = 0;

        if (~(activeCarCount=carCount)) {
            do addCars(2);
        }
        
        while (i < carCount) {
            let c = cars[i];
            if (c.isActive() & ~(rand(3)=0)) { // 66% chance to increase speed of each car
                do c.increaseSpeed(1);
            }
            let i = i + 1;
        }
        return;
    }
}