class Game {
    field int state; // 0=Menu, 1=Play, 2=Credits, 3=Instructions, 4=Leaderboard, 5=GameOver
    field int seed;  // To create psuedo-randomness for every run of the game
    field Player player;
    field Traffic traffic;
    field Leaderboard leaderboard;
    field int score;
    field int level;

    // String constants to prevent memory leaks
    field String s_title;
    field String s_menu_play;
    field String s_menu_instr;
    field String s_menu_cred;
    field String s_menu_lead;
    field String s_instr;
    field String s_goal;
    field String s_ctrl;
    field String s_dodge;
    field String s_aim;
    field String s_cred;
    field String s_devs;
    field String s_proj_name;
    field String s_back;
    field String s_level_init;
    field String s_score_init;
    field String s_game_over;
    field String s_final_score;
    field String s_new_highscore;
    field String s_enter_name;

    constructor Game new() {
        let state = 0;
        let seed = 0;
        let leaderboard = Leaderboard.new();
        let score = 0;
        let level = 1;

        let s_title = "HIT THE ROAD, JACK";
        let s_menu_play  = "1. Play Game";
        let s_menu_instr = "2. Instructions";
        let s_menu_cred  = "3. Credits";
        let s_menu_lead  = "4. Leaderboard";
        
        let s_instr = "INSTRUCTIONS:";
        let s_goal  = "Goal: Cross the road without getting squashed.";
        let s_ctrl  = "Controls: Use arrows / space to move up, down and forward.";
        let s_dodge = "Dodge the cars and reach the next Safe Zone.";
        let s_aim = "Aim for the highest score to place on the leaderboards.";
        
        let s_cred = "CREDITS:";
        let s_devs  = "Created by Omer Samson, Alon Levy and Ofek Marciano";
        let s_proj_name  = "Nand2Tetris Project 9";
        let s_back  = "Press any key to go back...";

        let s_level_init = "Level: 1";
        let s_score_init = "Score: 0";
        let s_game_over = "GAME OVER";
        let s_final_score = "Final Score: ";
        let s_new_highscore = "NEW HIGH SCORE!";
        let s_enter_name = "Enter Name for leaderboard: ";

        return this;
    }

    /** The Main Loop */
    method void run() {
        var boolean exit;
        let exit = false;

        while (~exit) {
            if (state = 0) { do runMenu(); }
            if (state = 1) { do runInstructions(); }
            if (state = 2) { do runCredits(); }
            if (state = 3) { do runLeaderboard(); }
            if (state = 4) { do runGame(); }
            if (state = 5) { do runGameOver(); }
        }
        return;
    }

    /** 0: Main Menu Loop */
    method void runMenu() {
        var char key;
        var boolean selected;
        var int animTimer;
        var int waveTime;
        
        do Screen.clearScreen();
        do Output.moveCursor(10, 20);
        do Output.printString(s_title);
        do Output.moveCursor(12, 20); 
        do Output.printString(s_menu_play);
        do Output.moveCursor(13, 20);
        do Output.printString(s_menu_instr);
        do Output.moveCursor(14, 20);
        do Output.printString(s_menu_cred);
        do Output.moveCursor(15, 20);
        do Output.printString(s_menu_lead);

        let selected = false;
        let animTimer = 0;
        let waveTime = 0;

        // Draw the initial static wave
        do Animation.drawWave(110, 180, waveTime);

        while (~selected) {
            let key = Keyboard.keyPressed();
            let seed = seed + 1;
            if (seed = 32767) { let seed = 0; }

            let animTimer = animTimer + 1;
            
            // Speed of the wave (Adjust > to make it faster/slower)
            if (animTimer > 10000) {
                let animTimer = 0;
                let waveTime = waveTime + 1;
                if (waveTime > 30000) { let waveTime = 0; }
                do Animation.drawWave(110, 180, waveTime);
            }

            // Handle Menu Keys
            if (key = 49) { let state = 4; let selected = true; } // 1 key
            if (key = 50) { let state = 1; let selected = true; } // 2 key
            if (key = 51) { let state = 2; let selected = true; } // 3 key
            if (key = 52) { let state = 3; let selected = true; } // 4 key
        }
        return;
    }

    /** 1: Instructions */
    method void runInstructions() {
        do Screen.clearScreen();
        do Output.moveCursor(4, 3);
        do Output.printString(s_instr);
        do Output.moveCursor(6, 4);
        do Output.printString(s_goal);
        do Output.moveCursor(7, 4);
        do Output.printString(s_ctrl);
        do Output.moveCursor(9, 4);
        do Output.printString(s_dodge);
        do Output.moveCursor(10, 4);
        do Output.printString(s_aim);
        do Output.moveCursor(12, 4);
        do Output.printString(s_back);
        
        do Sys.wait(500); 
        while (Keyboard.keyPressed() = 0) {}
        let state = 0; 
        return;
    }

    /** 2: Credits */
    method void runCredits() {
        do Screen.clearScreen();
        do Output.moveCursor(4, 3);
        do Output.printString(s_cred);
        do Output.moveCursor(6, 4);
        do Output.printString(s_devs);
        do Output.moveCursor(7, 4);
        do Output.printString(s_proj_name);
        do Output.moveCursor(9, 4);
        do Output.printString(s_back);
        
        do Sys.wait(500);
        while (Keyboard.keyPressed() = 0) {}
        let state = 0;
        return;
    }

    /** 3: Leaderboard */
    method void runLeaderboard() {
        do Screen.clearScreen();
        do leaderboard.show(20, 5);
        
        do Output.moveCursor(20, 20);
        do Output.printString(s_back);
        
        do Sys.wait(500);
        while (Keyboard.keyPressed() = 0) {}
        let state = 0;
        return;
    }

    /** 4: Game logic */
    method void runGame() {
        var char key;
        var boolean exitGame, levelComplete, okToMove;
        
        let exitGame = false;
        let okToMove = true;
        let score = 0;
        let level = 1;
        do Screen.clearScreen();

        do Sprites.drawRoad();

        // Draw HUD 
        do Output.moveCursor(0, 0);
        do Output.printString(s_level_init);
        do Output.moveCursor(1, 0);
        do Output.printString(s_score_init);
        
        let player = Player.new();
        let traffic = Traffic.new(seed);

        while (~exitGame) {
            // Update HUD 
            do Output.moveCursor(0, 7);
            do Output.printInt(level);
            do Output.moveCursor(1, 7);
            do Output.printInt(score);

            // Movement logic
            let key = Keyboard.keyPressed();
            if ((~okToMove) & key = 0) { let okToMove = true; }
            if ((~(key = 0)) & okToMove) {
                if (key = 131) { do player.moveUp(); }
                if (key = 133) { do player.moveDown(); }
                if ((key = 132) | (key = 32)) { 
                    do player.moveRight();
                    let score = score + 1;
                }
                if (key = 140) { let exitGame = true; let state = 5; } // ESC key to exit

                let okToMove = false;
            }
            
            // Level up logic
            if (player.getX() > 448) { // Past 14 * 32, the 12th lane
                let score = score + 2;
                let level = level + 1;
                
                do traffic.increaseDifficulty();
                do Animation.runLevelUp();

                do player.dispose();
                let player = Player.new(); 
                
                do Sys.wait(500);
            }
            
            // Car logic (movement and collision)
            do traffic.moveTraffic();
            if (traffic.checkCollisions(player)) {
                do Output.moveCursor(10, 20);
                do Output.printString(s_game_over);
                do Sys.wait(2000);
                let exitGame = true;
                let state = 5;
            }
            do Sys.wait(20);
        }

        do player.dispose();
        do traffic.dispose();
        return;
    }

    /** 5: Game Over logic */
    method void runGameOver() {
        var String name;

        do Screen.clearScreen();
        do Output.moveCursor(10, 25);
        do Output.printString(s_game_over);
        do Output.moveCursor(12, 22);
        do Output.printString(s_final_score);
        do Output.printInt(score);
        
        if (leaderboard.isHighScore(score)) {
            do Output.moveCursor(14, 15);
            do Output.printString(s_new_highscore);
            do Output.moveCursor(16, 15);
            let name = Keyboard.readLine(s_enter_name);
            
            do leaderboard.addScore(score, name);
        } else {
            do Output.moveCursor(15, 18);
            do Output.printString(s_back);
            while (~(Keyboard.keyPressed() = 128)) {}
            while (Keyboard.keyPressed() = 128) {}
        }

        let state = 0;
        return;
    }

    method void dispose() {
        do s_title.dispose();
        do s_menu_play.dispose();
        do s_menu_instr.dispose();
        do s_menu_cred.dispose();
        do s_menu_lead.dispose();
        do s_instr.dispose();
        do s_goal.dispose();
        do s_ctrl.dispose();
        do s_dodge.dispose();
        do s_aim.dispose();
        do s_cred.dispose();
        do s_devs.dispose();
        do s_proj_name.dispose();
        do s_back.dispose();
        do s_level_init.dispose();
        do s_score_init.dispose();
        do s_game_over.dispose();
        do s_final_score.dispose();
        do s_new_highscore.dispose();
        do s_enter_name.dispose();
        do Memory.deAlloc(this);
        return;
    }
}