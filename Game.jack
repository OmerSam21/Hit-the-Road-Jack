class Game {
    field int state; // 0=Menu, 1=Play, 2=Credits, 3=Instructions, 4=Leaderboard
    field int seed;  // To create psuedo-randomness for every run of the game
    field Player player;

    constructor Game new() {
        let state = 0;
        let seed = 0;
        return this;
    }

    /** The Main Loop */
    method void run() {
        var boolean exit;
        let exit = false;

        while (~exit) {
            if (state = 0) { do runMenu(); }
            if (state = 1) { do runInstructions(); }
            if (state = 2) { do runCredits(); }
            if (state = 3) { do runLeaderboard(); }
            if (state = 4) { do runGame(); }
        }
        return;
    }

    /** 0: Main Menu Loop */
    method void runMenu() {
        var char key;
        var boolean selected;
        
        do Screen.clearScreen();
        do Output.moveCursor(10, 20);
        do Output.printString("HIT THE ROAD, JACK");
        do Output.moveCursor(12, 20); 
        do Output.printString("1. Play Game");
        do Output.moveCursor(13, 20);
        do Output.printString("2. Instructions");
        do Output.moveCursor(14, 20);
        do Output.printString("3. Credits");
        do Output.moveCursor(15, 20);
        do Output.printString("4. Leaderboard");
        
        let selected = false;
        while (~selected) {
            let key = Keyboard.keyPressed();
            
            let seed = seed + 1;
            if (seed = 32767) { let seed = 0; }

            // Handle Menu Keys
            if (key = 49) { let state = 4; let selected = true; } // 1 key -> Play
            if (key = 50) { let state = 1; let selected = true; } // 2 key -> Instructions
            if (key = 51) { let state = 2; let selected = true; } // 3 key -> Credits
            if (key = 52) { let state = 3; let selected = true; } // 4 key -> Leaderboard
        }
        return;
    }

    /** 1: Instructions */
    method void runInstructions() {
        do Screen.clearScreen();
        do Output.moveCursor(4, 2);
        do Output.printString("INSTRUCTIONS:");
        do Output.moveCursor(6, 3);
        do Output.printString("Goal: Cross the road without getting squashed.");
        do Output.moveCursor(7, 3);
        do Output.printString("Controls: Use Arrow keys to move.");
        do Output.moveCursor(8, 3);
        do Output.printString("Dodge the cars and reach the next Safe Zone.");
        do Output.moveCursor(9, 3);
        do Output.printString("Aim for the highest score.");
        do Output.moveCursor(11, 3);
        do Output.printString("Press any key to go back...");
        
        do Sys.wait(500); 
        while (Keyboard.keyPressed() = 0) {}
        let state = 0; 
        return;
    }

    /** 2: Credits */
    method void runCredits() {
        do Screen.clearScreen();
        do Output.moveCursor(0, 0);

        do Output.printString("CREDITS:");
        do Output.println();
        do Output.println();
        do Output.printString("Created by Omer Samson, Alon Levy and Ofek Marciano");
        do Output.println();
        do Output.printString("For Nand2Tetris Project 9");
        do Output.println();
        do Output.println();
        do Output.printString("Press any key to go back...");
        
        do Sys.wait(500);
        while (Keyboard.keyPressed() = 0) {}
        let state = 0;
        return;
    }

    /** 3: Leaderboard */
    method void runLeaderboard() {
        do Screen.clearScreen();
        do Output.moveCursor(0, 0);
        // Placeholder of fake leaderboard for now
        do Output.printString("LEADERBOARD (Session):");
        do Output.println();
        do Output.println();
        do Output.printString("1. ALE - 067");
        do Output.println();
        do Output.printString("2. OFM - 000");
        do Output.println();
        do Output.println();
        do Output.printString("Press any key to go back...");
        
        do Sys.wait(500);
        while (Keyboard.keyPressed() = 0) {}
        let state = 0;
        return;
    }

    /** 4: Game logic */
    method void runGame() {
        var char key;
        var boolean exitGame;
        
        let exitGame = false;
        do Screen.clearScreen();
        
        let player = Player.new(0, 120, 16);

        while (~exitGame) {
            let key = Keyboard.keyPressed();
            
            if (key = 130) { do player.moveLeft(); }
            if (key = 131) { do player.moveUp(); }
            if (key = 132) { do player.moveRight(); }
            if (key = 133) { do player.moveDown(); }
            if (key = 140) { let exitGame = true; } // ESC key to exit

            do Sys.wait(5); // Wait 5ms. To control game speed.
        }

        do player.dispose();
        let state = 0;
        return;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }
}