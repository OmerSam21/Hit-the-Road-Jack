class Game {
    field int state; // 0=Menu, 1=Play, 2=Credits, 3=Instructions, 4=Leaderboard, 5=GameOver
    field int seed;  // To create psuedo-randomness for every run of the game
    field Player player;
    field Traffic traffic;
    field Leaderboard leaderboard;
    field int score;
    field int level;

    constructor Game new() {
        let state = 0;
        let seed = 0;
        let leaderboard = Leaderboard.new();
        let score = 0;
        let level = 1;
        return this;
    }

    /** The Main Loop */
    method void run() {
        var boolean exit;
        let exit = false;

        while (~exit) {
            if (state = 0) { do runMenu(); }
            if (state = 1) { do runInstructions(); }
            if (state = 2) { do runCredits(); }
            if (state = 3) { do runLeaderboard(); }
            if (state = 4) { do runGame(); }
            if (state = 5) { do runGameOver(); }
        }
        return;
    }

    /** 0: Main Menu Loop */
    method void runMenu() {
        var char key;
        var boolean selected;
        
        do Screen.clearScreen();
        do Output.moveCursor(10, 20);
        do Output.printString("HIT THE ROAD, JACK");
        do Output.moveCursor(12, 20); 
        do Output.printString("1. Play Game");
        do Output.moveCursor(13, 20);
        do Output.printString("2. Instructions");
        do Output.moveCursor(14, 20);
        do Output.printString("3. Credits");
        do Output.moveCursor(15, 20);
        do Output.printString("4. Leaderboard");
        
        let selected = false;
        while (~selected) {
            let key = Keyboard.keyPressed();
            
            let seed = seed + 1;
            if (seed = 32767) { let seed = 0; }

            // Handle Menu Keys
            if (key = 49) { let state = 4; let selected = true; } // 1 key -> Play
            if (key = 50) { let state = 1; let selected = true; } // 2 key -> Instructions
            if (key = 51) { let state = 2; let selected = true; } // 3 key -> Credits
            if (key = 52) { let state = 3; let selected = true; } // 4 key -> Leaderboard
        }
        return;
    }

    /** 1: Instructions */
    method void runInstructions() {
        do Screen.clearScreen();
        do Output.moveCursor(4, 3);
        do Output.printString("INSTRUCTIONS:");
        do Output.moveCursor(6, 4);
        do Output.printString("Goal: Cross the road without getting squashed.");
        do Output.moveCursor(7, 4);
        do Output.printString("Controls: Use Arrow keys to move.");
        do Output.moveCursor(9, 4);
        do Output.printString("Dodge the cars and reach the next Safe Zone.");
        do Output.moveCursor(10, 4);
        do Output.printString("Aim for the highest score to place on the leaderboards.");
        do Output.moveCursor(12, 4);
        do Output.printString("Press any key to go back...");
        
        do Sys.wait(500); 
        while (Keyboard.keyPressed() = 0) {}
        let state = 0; 
        return;
    }

    /** 2: Credits */
    method void runCredits() {
        do Screen.clearScreen();
        do Output.moveCursor(4, 3);
        do Output.printString("CREDITS:");
        do Output.moveCursor(6, 4);
        do Output.printString("Created by Omer Samson, Alon Levy and Ofek Marciano");
        do Output.moveCursor(7, 4);
        do Output.printString("For Nand2Tetris Project 9");
        do Output.moveCursor(9, 4);
        do Output.printString("Press any key to go back...");
        
        do Sys.wait(500);
        while (Keyboard.keyPressed() = 0) {}
        let state = 0;
        return;
    }

    /** 3: Leaderboard */
    method void runLeaderboard() {
        do Screen.clearScreen();
        do leaderboard.show(20, 5);
        
        do Output.moveCursor(20, 20);
        do Output.printString("Press any key to back...");
        
        do Sys.wait(500);
        while (Keyboard.keyPressed() = 0) {}
        let state = 0;
        return;
    }

    /** 4: Game logic */
    method void runGame() {
        var char key;
        var boolean exitGame, levelComplete, okToMove;
        
        let exitGame = false;
        let okToMove = true;
        let score = 0;
        let level = 1;
        do Screen.clearScreen();

        // Draw HUD 
        do Output.moveCursor(0, 0);
        do Output.printString("Level: 1");
        do Output.moveCursor(1, 0);
        do Output.printString("Score: 0");
        
        let player = Player.new();
        let traffic = Traffic.new();

        while (~exitGame) {
            // Update HUD 
            do Output.moveCursor(0, 7);
            do Output.printInt(level);
            do Output.moveCursor(1, 7);
            do Output.printInt(score);

            // Movement logic
            let key = Keyboard.keyPressed();
            if ((~okToMove) & key = 0) { let okToMove = true; }
            if ((~(key = 0)) & okToMove) {
                if (key = 131) { do player.moveUp(); }
                if (key = 133) { do player.moveDown(); }
                if ((key = 132) | (key = 32)) { 
                    do player.moveRight();
                    let score = score + 1;
                }
                if (key = 140) { let exitGame = true; } // ESC key to exit

                let okToMove = false;
            }
            
            // Level up logic
            if (player.getX() > 448) { // Past 14 * 32, the 12th lane
                let score = score + 2;
                let level = level + 1;
                
                do traffic.increaseDifficulty();

                do Sys.wait(500);
                do player.dispose();
                let player = Player.new(); 
                
                // In future add flash screen
                do Sys.wait(500);
            }
            
            // Car logic (movement and collision)
            do traffic.moveTraffic();
            if (traffic.checkCollisions(player)) {
                do Output.moveCursor(10, 20);
                do Output.printString("GAME OVER");
                do Sys.wait(2000);
                let exitGame = true;
                let state = 5;
            }
            do Sys.wait(20);
        }

        do player.dispose();
        do traffic.dispose();
        return;
    }

    method void runGameOver() {
        var String name;

        do Screen.clearScreen();
        do Output.moveCursor(10, 25);
        do Output.printString("GAME OVER");
        do Output.moveCursor(12, 22);
        do Output.printString("Final Score: ");
        do Output.printInt(score);
        
        if (leaderboard.isHighScore(score)) {
            do Output.moveCursor(14, 15);
            do Output.printString("NEW HIGH SCORE!");
            do Output.moveCursor(16, 15);
            let name = Keyboard.readLine("Enter Name for leaderboard: ");
            
            do leaderboard.addScore(score, name);
        } else {
            do Output.moveCursor(15, 18);
            do Output.printString("Press ENTER to try again");
            while (~(Keyboard.keyPressed() = 128)) {}
            while (Keyboard.keyPressed() = 128) {}
        }

        let state = 0;
        return;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }
}