class Car {
    field int x; // The horizontal lane position
    field int y; // The vertical position (changes)
    field int width; // How wide the car is
    field int height; // How long the car is
    field int speed; // How fast it moves
    field int direction; // 1 = Down, -1 = Up
    field boolean active; // If car is active onscreen

    constructor Car new(int laneX, int startY, int w, int h, int s, int dir) {
        let x = laneX;
        let y = startY;
        let width = w;
        let height = h;
        let speed = s;
        let direction = dir;
        let active = false;
        return this;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    // Getters
    method int getX() { return x; }
    method int getY() { return y; }
    method int getWidth() { return width; }
    method int getHeight() { return height; }
    method boolean isActive() { return active; }

    /** Turn the car active */
    method void activate() {
        let active = true;
        do draw();
        return;
    }

    /** Draws the car based on coords */
    method void draw() {
        if (active) { 
            do Sprites.drawCar(x, y, true); 
        }
        return;
    }

    /** Erases the car before moving */
    method void erase() {
        if (active) {
            do Sprites.drawCar(x, y, false);
        }
        return;
    }

    /** Updates the car's position */
    method void move() {
        if (active) {
            do erase();

            // Update Y based on speed and direction
            let y = y + (speed * direction);

            // Wrap around if out of bounds
            if ((direction = 1) & (y > 256)) {
                let y = -32;
            }
            if ((direction = -1) & (y < -32)) {
                let y = 256;
            }

            do draw();
        }
        return;
    }

    /** Returns true if this car is hitting the player */
    method boolean checkCollision(int playerX, int playerY, int playerSize) {
        var int pLeft, pRight, pTop, pBottom;
        var int cLeft, cRight, cTop, cBottom;
        var boolean collision;
        var int padding;

        if (~active) { return false; }

        let padding = 8;

        // Player coordinates
        let pLeft = playerX;
        let pRight = playerX + playerSize;
        let pTop = playerY + padding;
        let pBottom = playerY + playerSize + padding;

        // Car coordinates
        let cLeft = x;
        let cRight = x + width;
        let cTop = y + 2;
        let cBottom = y + height;

        let collision = true;

        // Check for NO overlap
        if (pBottom < cTop) { let collision = false; }  // Player is above Car
        if (pTop > cBottom) { let collision = false; }  // Player is below Car
        if (pRight < cLeft) { let collision = false; }  // Player is left of Car
        if (pLeft > cRight) { let collision = false; }  // Player is right of Car

        return collision;
    }

    // Increase on speed every level
    method void increaseSpeed(int amount) {
        let speed = speed + amount;
        return;
    }
}