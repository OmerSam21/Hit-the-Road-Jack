class Animation {

    static String s_lvl_up;
    static String s_lvl_up_score;

    function void init() {
        let s_lvl_up = "LEVEL UP!";
        let s_lvl_up_score = "Score +3";
        return;
    }
    function void drawFrame(int x, int y, int frame) {
        var int location;
        
        let location = (y * 32) + (x / 16);

        if (frame = 0) { do Sprites.drawIdle(location); }
        if (frame = 1) { do Sprites.drawMid(location); }
        if (frame = 2) { do Sprites.drawJump(location); }
        
        return;
    }

    function void drawWave(int x, int y, int timeStep) {
        var int i;
        var int currentFrame;
        var int step;
        
        let i = 0;
        while (i < 6) {
            // Calculate the animation frame based on Time + Position
            let step = timeStep + i;
            
            // Modulo 3 logic: frame = step % 3
            let currentFrame = step - (3 * (step / 3));
            
            // Draw the frog. Spacing is 48 pixels (32 width + 16 gap)
            do Animation.drawFrame(x + (i * 48), y, currentFrame);
            
            let i = i + 1;
        }
        return;
    }

    /** Animation for Level Up event */
    function void runLevelUp() {
        var int i;
        
        // Draw a Pop-up Box
        do Screen.setColor(false);
        do Screen.drawRectangle(170, 90, 342, 160);
        
        do Screen.setColor(true); 
        // Draw Borders
        do Screen.drawRectangle(170, 90, 342, 92);   
        do Screen.drawRectangle(170, 158, 342, 160);
        do Screen.drawRectangle(170, 90, 172, 160);
        do Screen.drawRectangle(340, 90, 342, 160);
        
        // Flash the Text
        let i = 0;
        while (i < 4) {
            do Output.moveCursor(10, 27); 
            do Output.printString(s_lvl_up);
            
            do Output.moveCursor(12, 28); 
            do Output.printString(s_lvl_up_score);
            
            do Sys.wait(400);
            
            do Screen.setColor(false);
            do Screen.drawRectangle(200, 110, 310, 150); 
            do Screen.setColor(true);
            
            do Sys.wait(300);
            
            let i = i + 1;
        }
        
        // Restore the Game View
        do Screen.setColor(false);
        do Screen.drawRectangle(170, 90, 342, 160);
        
        // Redraw the road lines
        do Sprites.drawRoad();
        
        return;
    }
    method void dispose() {
        do s_lvl_up.dispose();
        do s_lvl_up_score.dispose();
        return;
    }
}