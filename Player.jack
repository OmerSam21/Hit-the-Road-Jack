class Player {
    field int x, y; // Top-left corner of the player
    field int size; // Size of the player
    field int jumpSize; // Size of each jump

    /** Creates a new player at the given location. */
    constructor Player new() {
        let x = 32;
        let y = 120;
        let size = 16;
        let jumpSize = 32;
        do draw();
        return this;
    }

    method void dispose() {
        do erase();
        do Memory.deAlloc(this);
        return;
    }

    /** Draws the player using the Sprites class. */
    method void draw() {
        do Sprites.drawPlayer(x, y, true);
        return;
    }

    /** Erases the player (prepares for movement). */
    method void erase() {
        do Sprites.drawPlayer(x, y, false);
        return;
    }

    /** Movement Methods with Boundary Checks */
    
    method void moveUp() {
        if (y > jumpSize) {
            do erase();
            let y = y - jumpSize;
            do draw();
        }
        return;
    }

    method void moveDown() {
        if ((y + size + jumpSize) < 256) {
            do erase();
            let y = y + jumpSize;
            do draw();
        }
        return;
    }

    method void moveRight() {
        if ((x + size + jumpSize) < 512) {
            do erase();
            let x = x + jumpSize;
            do draw();
        }
        return;
    }

    // Getters for collision detection
    method int getX() { return x; }
    method int getY() { return y; }
    method int getSize() { return size; }
}